pluginManagement {
	repositories {
		mavenLocal()
		maven { name = '阿里云'; url = 'https://maven.aliyun.com/repository/public/' }
		gradlePluginPortal()
		mavenCentral()
	}
}

plugins {
	id 'io.spring.dependency-management' version "$dependencyManagementPluginVersion" apply false
	id 'org.springframework.boot' version "$springBootVersion" apply false
	id 'com.gorylenko.gradle-git-properties' version "$gitPluginVersion" apply false
	id 'dev.yumi.gradle.licenser' version "$licensePluginVersion" apply false
}

dependencyResolutionManagement {
	repositories {
		mavenLocal()
		maven { name = '阿里云'; url = 'https://maven.aliyun.com/repository/public/' }
		mavenCentral()
		gradlePluginPortal()
	}

	repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'spring-batch-demo'

include ':project-bom'

['projects-app', 'projects-lib'].each {
	file(it).eachDir { projectDir ->
		if (projectDir.directory && new File(projectDir, 'build.gradle').exists()) {
			include "$it:${projectDir.name}"
		}
	}
}
